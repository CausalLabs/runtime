plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.5'
    api ('org.apache.avro:avro:1.11.0') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat'
        exclude group: 'org.codehaus.jackson'
    }
    api 'org.slf4j:slf4j-api:1.7.30'
}

compileJava {
    options.release = 8
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            url = uri("s3://maven.causallabs.io/")
            authentication {
              awsIm(AwsImAuthentication)
            }
        }
    }
}
